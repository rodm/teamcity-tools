
plugins {
    id 'com.github.rodm.teamcity-agent' version '0.8.3'
    id 'com.github.rodm.teamcity-server' version '0.8.3'
}

group = 'com.github.rodm.teamcity'
version = '1.0'

ext {
    serversDir = project.hasProperty('servers.dir') ? property('servers.dir') : "$rootDir/servers"
}

repositories {
    ivy {
        artifactPattern 'https://sourceforge.net/projects/[organization]/files/[organization]/[revision]/[module]-[revision].[ext]/download'
    }
}

configurations {
    findbugs
}

dependencies {
    findbugs group: 'findbugs', name: 'findbugs-noUpdateChecks', version: '3.0.1', ext: 'zip'
}

agentPlugin {
    baseName = 'findbugs3_0'
    version = ''
}

serverPlugin {
    baseName = 'findbugs3_0-tool'
    version = ''
}

teamcity {
    agent {
        descriptor {
            toolDeployment {
                executableFiles {
                    include 'bin/findbugs'
                }
            }
        }

        files {
            from(zipTree(configurations.findbugs.singleFile)) {
                includeEmptyDirs = false
                eachFile { file ->
                    file.relativePath = new RelativePath(file.relativePath.endsWithFile, file.relativePath.segments[1..-1] as String[])
                }
            }
        }
    }

    server {
        descriptor {
            name = 'findbugs3_0-tool'
            displayName = 'Repackaged FindBugs 3.0'
            version = project.version
            vendorName = 'rodm'
            description = 'FindBugs 3.0 repackaged for TeamCity'
            downloadUrl = 'https://github.com/rodm/teamcity-tools/'
            email = 'rod.n.mackenzie@gmail.com'
            vendorUrl = 'https://github.com/rodm/teamcity-tools/'
        }
    }

    environments {
        teamcity {
            version = '9.1.6'
            homeDir = file("$serversDir/TeamCity-${version}")
        }
    }
}
