
plugins {
    id 'org.gradle.maven-publish'
    id 'com.github.rodm.teamcity-agent' version '0.10'
    id 'com.github.rodm.teamcity-server' version '0.10'
    id 'com.jfrog.bintray' version '1.7.2'
}

group = 'com.github.rodm.teamcity'
version = '3.0.1'

ext {
    toolName = 'findbugs3_0-tool'
    toolUrl = 'https://github.com/rodm/teamcity-tools'
    toolDescription = 'FindBugs 3.0 repackaged for TeamCity'

    serversDir = findProperty('servers.dir') ?: "$rootDir/servers"

    repositoryUrl = findProperty('repositoryUrl') ?: 'http://localhost:8080'
    repositoryUsername = findProperty('repositoryUsername') ?: ''
    repositoryPassword = findProperty('repositoryPassword') ?: ''
}

repositories {
    ivy {
        artifactPattern 'https://sourceforge.net/projects/[organization]/files/[organization]/[revision]/[module]-[revision].[ext]/download'
    }
}

configurations {
    findbugs
}

dependencies {
    findbugs group: 'findbugs', name: 'findbugs-noUpdateChecks', version: version, ext: 'zip'
}

agentPlugin {
    baseName = project.toolName.replace('-tool', '')
    version = ''
}

serverPlugin {
    baseName = project.toolName
    version = ''
}

teamcity {
    agent {
        descriptor {
            toolDeployment {
                executableFiles {
                    include 'bin/findbugs'
                }
            }
        }

        files {
            from(zipTree(configurations.findbugs.singleFile)) {
                includeEmptyDirs = false
                eachFile { file ->
                    file.relativePath = new RelativePath(file.relativePath.endsWithFile, file.relativePath.segments[1..-1] as String[])
                }
            }
        }
    }

    server {
        descriptor {
            name = project.toolName
            displayName = 'Repacked FindBugs 3.0'
            version = project.version
            vendorName = 'rodm'
            vendorUrl = project.toolUrl
            description = project.toolDescription
            downloadUrl = project.toolUrl
            email = 'rod.n.mackenzie@gmail.com'
        }

        baseHomeDir = project.serversDir

        environments {
            teamcity8 {
                version = '8.1.5'
            }
            teamcity9 {
                version = '9.1.7'
            }
            teamcity10 {
                version = '10.0.2'
            }
        }
    }
}

artifacts {
    archives serverPlugin
}

publishing {
    publications {
        plugin(MavenPublication) {
            artifactId project.toolName
            artifact serverPlugin
        }
    }
    repositories {
        maven {
            url repositoryUrl
            credentials {
                username repositoryUsername
                password repositoryPassword
            }
        }
    }
}

bintray {
    user = findProperty('bintray.user')
    key = findProperty('bintray.key')

    filesSpec {
        from "${buildDir}/distributions"
        into project.version
        include '*-tool.zip'
    }

    dryRun = false
    publish = true
    override = false

    pkg {
        repo = 'teamcity-plugins-generic'
        name = project.toolName
        desc = project.toolDescription
        websiteUrl = project.toolUrl
        issueTrackerUrl = "${project.toolUrl}/issues"
        vcsUrl = "${project.toolUrl}.git"
        licenses = ['Apache-2.0']
        labels = ['teamcity', 'plugin', 'tool', 'findbugs']

        version {
            name = project.version
        }
    }
}
