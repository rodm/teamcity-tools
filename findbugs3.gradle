
plugins {
    id 'org.gradle.maven-publish'
    id 'com.github.rodm.teamcity-agent' version '0.9.1'
    id 'com.github.rodm.teamcity-server' version '0.9.1'
}

group = 'com.github.rodm.teamcity'
version = '3.0.1'

ext {
    serversDir = findProperty('servers.dir') ?: "$rootDir/servers"

    repositoryUrl = findProperty('repositoryUrl') ?: 'http://localhost:8080'
    repositoryUsername = findProperty('repositoryUsername') ?: ''
    repositoryPassword = findProperty('repositoryPassword') ?: ''
}

repositories {
    ivy {
        artifactPattern 'https://sourceforge.net/projects/[organization]/files/[organization]/[revision]/[module]-[revision].[ext]/download'
    }
}

configurations {
    findbugs
}

dependencies {
    findbugs group: 'findbugs', name: 'findbugs-noUpdateChecks', version: version, ext: 'zip'
}

agentPlugin {
    baseName = 'findbugs3_0'
    version = ''
}

serverPlugin {
    baseName = 'findbugs3_0-tool'
    version = ''
}

teamcity {
    agent {
        descriptor {
            toolDeployment {
                executableFiles {
                    include 'bin/findbugs'
                }
            }
        }

        files {
            from(zipTree(configurations.findbugs.singleFile)) {
                includeEmptyDirs = false
                eachFile { file ->
                    file.relativePath = new RelativePath(file.relativePath.endsWithFile, file.relativePath.segments[1..-1] as String[])
                }
            }
        }
    }

    server {
        descriptor {
            name = 'findbugs3_0-tool'
            displayName = 'Repacked FindBugs 3.0'
            version = project.version
            vendorName = 'rodm'
            description = 'FindBugs 3.0 repackaged for TeamCity'
            downloadUrl = 'https://github.com/rodm/teamcity-tools/'
            email = 'rod.n.mackenzie@gmail.com'
            vendorUrl = 'https://github.com/rodm/teamcity-tools/'
        }

        baseHomeDir = project.serversDir

        environments {
            teamcity8 {
                version = '8.1.5'
            }
            teamcity9 {
                version = '9.1.7'
            }
            teamcity10 {
                version = '10.0.2'
            }
        }
    }
}

artifacts {
    archives serverPlugin
}

publishing {
    publications {
        plugin(MavenPublication) {
            artifactId 'findbugs3_0-tool'
            artifact serverPlugin
        }
    }
    repositories {
        maven {
            url repositoryUrl
            credentials {
                username repositoryUsername
                password repositoryPassword
            }
        }
    }
}
